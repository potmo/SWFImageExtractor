<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   width="336"
					   height="398"
					   creationComplete="onCreationComplete(event)">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>

	<fx:Script>
		<![CDATA[
			import com.potmo.imagextractor.FramePacker;
			import com.potmo.imagextractor.PacketFrames;
			import com.potmo.imagextractor.Rasterizer;
			import com.potmo.util.image.BitmapUtil;
			import com.potmo.util.image.RasterizedSequence;
			import com.potmo.util.logger.Logger;
			import com.potmo.util.text.TextUtil;

			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.flash.ContainerMovieClip;
			import mx.graphics.codec.PNGEncoder;
			import mx.managers.DragManager;

			private var inputFile:File; // swf file
			private var exportLocation:File; // directory

			private var printWhenLoaded:Boolean;
			[Bindable]
			private var outputListItems:ArrayCollection;


			protected function onCreationComplete( event:FlexEvent ):void
			{
				addEventListener( NativeDragEvent.NATIVE_DRAG_ENTER, onDragEnter );
				addEventListener( NativeDragEvent.NATIVE_DRAG_DROP, onDrop );
			}


			private function onDrop( event:NativeDragEvent ):void
			{
				//get the array of files
				var fileArray:Array = event.clipboard.getData( ClipboardFormats.FILE_LIST_FORMAT ) as Array;
				var files:Vector.<File> = Vector.<File>( fileArray );

				inputFile = files[ 0 ];
				Logger.log( "Drop: " + inputFile.url );

				inputFileLabel.text = inputFile.name;

				printWhenLoaded = false;
				loadSwfFile( inputFile );
			}


			private function onDragEnter( event:NativeDragEvent ):void
			{
				//check and see if files are being drug in
				if ( !event.clipboard.hasFormat( ClipboardFormats.FILE_LIST_FORMAT ) )
				{
					Logger.error( "Wrong cliboard format" );
					return;
				}

				//get the array of files
				var fileArray:Array = event.clipboard.getData( ClipboardFormats.FILE_LIST_FORMAT ) as Array;
				var files:Vector.<File> = Vector.<File>( fileArray );

				//make sure only one file is dragged in (i.e. this app doesn't
				//support dragging in multiple files)
				if ( files.length != 1 )
				{
					Logger.error( "too many files: " + files.length );
					DragManager.showFeedback( DragManager.NONE );
					return;
				}

				if ( files[ 0 ].extension != "swf" )
				{
					Logger.error( "wrong extension: " + files[ 0 ].extension );
					DragManager.showFeedback( DragManager.NONE );
					return;
				}

				//accept the drag action
				Logger.log( "OK to drop" );
				DragManager.showFeedback( DragManager.COPY );
				DragManager.acceptDragDrop( this );

			}


			private function readFile( file:File ):ByteArray
			{
				//create a FileStream to work with the file
				var fileStream:FileStream = new FileStream();

				//open the file for reading
				fileStream.open( file, FileMode.READ );

				//read the bytes
				var data:ByteArray = new ByteArray();
				fileStream.readBytes( data, 0, fileStream.bytesAvailable );

				//close the file
				fileStream.close();

				return data;
			}


			private function loadSwfFile( file:File ):DisplayObjectContainer
			{
				Logger.log( "Loading swf file" );
				var loader:Loader = new Loader();
				var data:ByteArray = readFile( file );
				var context:LoaderContext = new LoaderContext( false, ApplicationDomain.currentDomain, null );
				context.allowCodeImport = true;
				loader.loadBytes( data, context );

				loader.contentLoaderInfo.addEventListener( Event.INIT, onLoadedSwfpartiallyAccessable );

				return loader;
			}


			private function printChildren( container:DisplayObjectContainer ):void
			{
				if ( container == null )
				{
					Logger.error( "Nothing to print" );
					return;
				}
				Logger.log( "Printing children" );

				var displayObjects:Vector.<DisplayObject> = new Vector.<DisplayObject>();

				for ( var i:int = 0; i < container.numChildren; i++ )
				{
					var object:DisplayObject = container.getChildAt( i );

					displayObjects.push( object );
				}

				if ( imagesCheckbox.selected )
				{
					Logger.log( "Printing images" );

					for each ( var child:DisplayObject in displayObjects )
					{

						Logger.log( "Child: " + i + " " + child.name );

						var sequence:RasterizedSequence = new RasterizedSequence( child );

						printSequenceToFile( child.name, sequence );
					}
				}

				if ( atlasCheckbox.selected )
				{
					Logger.log( "Printing atlas" );
					var packer:FramePacker = new FramePacker();

					Logger.info( "saving packed image" );
					var atlas:PacketFrames = packer.rasterizeAndPack( displayObjects );

					var encoder:PNGEncoder = new PNGEncoder();

					var imgByteArray:ByteArray = encoder.encode( atlas.getImage() );

					saveBytesToFile( imgByteArray, exportLocation.url, "atlas.png" );

					var xmlByteArray:ByteArray = new ByteArray();
					xmlByteArray.writeUTF( atlas.getXML().toXMLString() );
					saveBytesToFile( xmlByteArray, exportLocation.url, "atlas.xml" );
				}

			}


			protected function onLoadedSwfpartiallyAccessable( event:Event ):void
			{
				var moduleLoader:LoaderInfo = LoaderInfo( event.target );
				moduleLoader.addEventListener( Event.COMPLETE, onSwfAccessable );

			}


			protected function onSwfAccessable( event:Event ):void
			{
				var loader:Loader = event.target.loader;

				var swfToPrint:Sprite = loader.getChildAt( 0 ) as Sprite;

				outputListItems = new ArrayCollection();

				for ( var i:int = 0; i < swfToPrint.numChildren; i++ )
				{
					outputListItems.addItem( swfToPrint.getChildAt( i ).name );
				}

				if ( printWhenLoaded )
				{
					printWhenLoaded = false;
					printChildren( swfToPrint );
				}

			}


			private function printSequenceToFile( clipName:String, sequence:RasterizedSequence ):void
			{
				var images:Vector.<BitmapData> = sequence.getImages();
				var labels:Vector.<String> = sequence.getLables();
				var pngenc:PNGEncoder = new PNGEncoder();

				for ( var i:int = 0; i < images.length; i++ )
				{

					var image:BitmapData = images[ i ];
					var label:String = labels[ i ];
					var imgByteArray:ByteArray = pngenc.encode( image );

					saveBytesToFile( imgByteArray, exportLocation.url + "/" + clipName, TextUtil.prependZeroes( i.toString(), 5 ) + "_" + label + ".png" );
				}

			}


			private function saveBytesToFile( imgByteArray:ByteArray, folder:String, fileName:String ):void
			{
				//var fl:File = File.desktopDirectory.resolvePath( folder + "/" + fileName + ".png" );

				var fl:File = new File( folder + "/" + fileName );

				Logger.log( "Save to file: " + fl.url );

				var fs:FileStream = new FileStream();

				try
				{
					//open file in write mode
					fs.open( fl, FileMode.WRITE );
					//write bytes from the byte array
					fs.writeBytes( imgByteArray );
					//close the file
					fs.close();
				}
				catch ( e:Error )
				{
					Logger.error( e.message );
				}
			}


			protected function onExportClicked( event:MouseEvent ):void
			{
				if ( !inputFile )
				{
					return;
				}

				if ( exportLocation == null )
				{
					showBrowseForExportLocation();
					return;
				}

				printWhenLoaded = true;
				loadSwfFile( inputFile );

			}


			protected function outputClicked( event:MouseEvent ):void
			{
				showBrowseForExportLocation();

			}


			private function showBrowseForExportLocation():void
			{
				var file:File = new File();
				file.browseForDirectory( "Export to location" );
				file.addEventListener( Event.SELECT, onSelectExportLocation );

			}


			private function onSelectExportLocation( event:Event ):void
			{
				exportLocation = ( event.target ) as File;
				exportLocationInput.text = exportLocation.url;
				exportButton.label = "Export";

			}
		]]>
	</fx:Script>

	<s:Label id="inputFileLabel"
			 left="10"
			 right="10"
			 top="10"
			 height="25"
			 backgroundColor="#C4C4C4"
			 text="Drag swf file here"
			 textAlign="left"
			 verticalAlign="middle"/>
	<s:Button right="10"
			  bottom="10"
			  label="Browse export localtion"
			  click="onExportClicked(event)"
			  id="exportButton"/>
	<s:Label id="exportLocationInput"
			 left="10"
			 right="10"
			 bottom="43"
			 height="20"
			 click="outputClicked(event)"/>
	<s:List left="10"
			right="10"
			top="43"
			bottom="86"
			dataProvider="{outputListItems}">
	</s:List>
	<s:CheckBox id="atlasCheckbox"
				left="10"
				bottom="65"
				width="62"
				label="Atlas"
				selected="true"/>
	<s:CheckBox id="imagesCheckbox"
				left="76"
				bottom="65"
				label="images"/>
</s:WindowedApplication>
